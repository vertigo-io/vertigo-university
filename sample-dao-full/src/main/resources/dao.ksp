package io.vertigo.samples

create Task TkGetMoviesByCriteria {
    className : "io.vertigo.dynamox.task.TaskEngineSelect"
        request : "
        	select mov.*
        	from movie mov
        	where 
        	1=1
        	<%if(title != null) {%>
        	 and mov.NAME like concat(#title#, '%%')
        	<%}%>
        	<%if(year != null) {%>
        	 and mov.YEAR = #year#
        	<%}%>
			"
	in title	 	{domain : DoLabelLong 		cardinality:"1" 	}
	in year	 		{domain : DoYear 			cardinality:"1" 	}
	out movies		{domain : DoDtMovie 		cardinality:"*" 	}
}

create Task TkGetMoviesByCriteriaWithCountry {
    className : "io.vertigo.dynamox.task.TaskEngineSelect"
        request : "
        	select mov.*
        	from movie mov
        	where 
        	1=1
        	<%if(title != null) {%>
        	 and mov.NAME like concat(#title#, '%%')
        	<%}%>
        	<%if(year != null) {%>
        	 and mov.YEAR = #year#
        	<%}%>
        	<%if(!countries.isEmpty()) {%>
        	 and mov.COU_ID in (#countries.rownum.couId#)
        	<%}%>
			"
	in title	 	{domain : DoLabelLong 			cardinality:"1" 	}
	in year	 		{domain : DoYear 				cardinality:"?"		}
	in countries	{domain : DoDtCountry 			cardinality:"*" 	}
	out movies		{domain : DoDtMovie 			cardinality:"*" 	}
}


create Task TkGetActorsInMovie {
    className : "io.vertigo.dynamox.task.TaskEngineSelect"
        request : "
        	select act.*
        	from role rol
        	join actor act on act.ACT_ID = rol.ACT_ID
        	where rol.MOV_ID = #movId#
			"
	in movId		{domain : DoId 			cardinality:"1" 	}
	out actors		{domain : DoDtActor 	cardinality:"*" 	}
}


create Task TkGetMoviesWith100Actors {
    className : "io.vertigo.dynamox.task.TaskEngineSelect"
        request : "
        	select mov.*
			from movie mov
			join (select MOV_ID, count(*) cnt from role group by mov_id ) hv on hv.MOV_ID = mov.MOV_ID
			where hv.cnt> 100
			"
	out movies	{domain : DoDtMovie 	cardinality:"*" }
}


create Task TkGetMovieDisplay {
    className : "io.vertigo.dynamox.task.TaskEngineSelect"
        request : "
		select mov.NAME,mov.YEAR, cou.NAME as COUNTRY, hv.CNT as ACTORS_COUNT
		from movie mov
		join country cou on cou.COU_ID = mov.COU_ID
		join (select MOV_ID, count(*) cnt from role group by mov_id ) hv on hv.MOV_ID = mov.MOV_ID
		where mov.YEAR > 2010
		limit 500
			"
	out movies	{domain : DoDtMovieDisplay 	cardinality:"*" }
}

create Task TkGetMovieByYear {
    className : "io.vertigo.dynamox.task.TaskEngineSelect"
        request : "
		select YEAR, count(*) as MOVIES_COUNT from movie where YEAR is not null group by year order by YEAR asc
			"
	out movies	{domain : DoDtMovieByYear 	cardinality:"*" 	}
}




